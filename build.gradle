buildscript {
    repositories {
        jcenter()
        maven {
            url = "https://repo.maven.apache.org/maven2"
        }
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
plugins {
    id 'com.github.johnrengelman.shadow' version '4.0.4'
    id 'java'
}
apply plugin: 'net.minecraftforge.gradle.forge'

version = "${project.mc_version}-${project.mod_version}"
group= "${project.maven_group}" 
archivesBaseName = "${project.mod_id}"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "${project.mc_version}-${project.forge_version}"
    runDir = "run"
    mappings = "${project.mcp_mappings}"
    replace "@VERSION@", "${project.mod_version}"
}

repositories {
   flatDir {
       dirs 'libs'
   }
}

dependencies {
    shadow "org.pircbotx:pircbotx:2.3-SNAPSHOT"
    shadow "org.slf4j:slf4j-api:1.7.13"
    shadow "org.slf4j:slf4j-nop:1.7.13"
    shadow "com.google.guava:guava:27.0-jre"
    shadow "org.apache.commons:commons-lang3:3.4"
}

shadowJar {
    classifier = ''
    relocate "com.google", "shadow.twitchplays.google"
    relocate "org.apache.commons", "shadow.twitchplays.apache.commons"
    relocate "org.slf4j", "shadow.twitchplays.slf4j"
}

reobf {
    shadowJar { mappingType = 'SEARGE' }
}

tasks.build.dependsOn reobfShadowJar

artifacts {
    archives shadowJar
}

processResources {
    inputs.property "version", "${project.mod_version}"
    inputs.property "mcversion", "${project.mc_version}"

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        expand 'version':"${project.version}", 'mcversion':"${project.mc_version}"
    }
        
  
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    classifier = 'slim'
	manifest.mainAttributes(
            "Implementation-Title": archivesBaseName,
            "Implementation-Version": project.version,
            "FMLAT": "twitchplays_at.cfg")
}