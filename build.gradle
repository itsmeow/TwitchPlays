buildscript {
    repositories {
        jcenter()
        maven {
            url = "https://repo.maven.apache.org/maven2"
        }
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
plugins {
    id 'com.github.johnrengelman.shadow' version '4.0.4'
    id 'java'
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'eclipse'

version = "${project.mc_version}-${project.mod_version}"
group= "${project.maven_group}" 
archivesBaseName = "${project.mod_id}"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "${project.mc_version}-${project.forge_version}"
    runDir = "run"
    mappings = "${project.mcp_mappings}"
    replace "@VERSION@", "${project.mod_version}"
}

repositories {
   flatDir {
       dirs 'libs'
       dirs 'src/main/resources/META-INF/libraries'
   }
}

dependencies {
    shadow "org.pircbotx:pircbotx:2.1"
    compile "org.slf4j:slf4j-api:1.7.13"
    compile "org.slf4j:slf4j-nop:1.7.13"
}

shadowJar {
    classifier = ''
    configurations = [project.configurations.shadow]
    relocate("com.google", "shadow.twitchplays.google") {
    	include 'org.pircbotx'
    }
    relocate("org.apache.commons", "shadow.twitchplays.apache.commons") {
    	include 'org.pircbotx'
    }
    exclude "org/slf4j/**"
}

task renameFromJar {
    doLast {
        ant.move(todir: "build/resources/main/META-INF/libraries") {
            fileset(dir: "build/resources/main/META-INF/libraries")
            mapper(type: "glob", from: "*.jar", to: "*.jardep")
        }
    }
}
shadowJar.dependsOn renameFromJar

task renameToJar {
    doLast {
        ant.unzip(src: "build/libs/${project.archivesBaseName}-${project.version}.jar", dest: "build/libs/tmpfatjar")
        ant.delete(file: "build/libs/${project.archivesBaseName}-${project.version}.jar")
        ant.move(todir: "build/libs/tmpfatjar/META-INF/libraries") {
            fileset(dir: "build/libs/tmpfatjar/META-INF/libraries")
            mapper(type: "glob", from: "*.jardep", to: "*.jar")
        }
        ant.zip(basedir: "build/libs/tmpfatjar", destfile: "build/libs/${project.archivesBaseName}-${project.version}.jar", update: true)
        ant.delete(dir: "build/libs/tmpfatjar")
    }
}

reobf {
    shadowJar { mappingType = 'SEARGE' }
}

tasks.build.dependsOn reobfShadowJar
tasks.build.dependsOn renameToJar

artifacts {
    archives shadowJar
}

processResources {
    inputs.property "version", "${project.mod_version}"
    inputs.property "mcversion", "${project.mc_version}"

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        expand 'version':"${project.version}", 'mcversion':"${project.mc_version}"
    }
        
  
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    classifier = 'slim'
	manifest.mainAttributes(
            "Implementation-Title": archivesBaseName,
            "Implementation-Version": project.version,
            "FMLAT": "twitchplays_at.cfg",
            "ContainedDeps": "slf4j-api-1.7.13.jar slf4j-nop-1.7.13.jar")
}